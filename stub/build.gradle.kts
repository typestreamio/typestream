import com.google.protobuf.gradle.id

plugins {
    id("typestream.kotlin-conventions")
    id("com.google.protobuf") version "0.9.4"
}

repositories {
    mavenCentral()
    google()
}

val grpcVersion: String = libs.versions.grpc.get()
val grpcKotlinVersion: String = libs.versions.grpcKotlin.get()
val protobufVersion: String = libs.versions.protobuf.get()

dependencies {
    protobuf(project(":protos"))
    api(kotlin("stdlib"))

    api("io.grpc:grpc-stub:$grpcVersion")
    api("io.grpc:grpc-protobuf:$grpcVersion")
    api("com.google.protobuf:protobuf-java-util:$protobufVersion")
    api("com.google.protobuf:protobuf-kotlin:$protobufVersion")
    api("io.grpc:grpc-kotlin-stub:$grpcKotlinVersion")
}

sourceSets {
    val main by getting { }
    main.java.srcDirs("build/generated/source/proto/main/java")
    main.java.srcDirs("build/generated/source/proto/main/grpc")
    main.java.srcDirs("build/generated/source/proto/main/kotlin")
    main.java.srcDirs("build/generated/source/proto/main/grpckt")
}


kotlin {
    target {
        compilerOptions {
            freeCompilerArgs = listOf("-opt-in=kotlin.RequiresOptIn")
        }
    }
}

project.protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:$protobufVersion"
    }
    plugins {
        id("grpc") {
            artifact = "io.grpc:protoc-gen-grpc-java:$grpcVersion"
        }
        id("grpckt") {
            artifact = "io.grpc:protoc-gen-grpc-kotlin:$grpcKotlinVersion:jdk8@jar"
        }
        id("doc") {
            artifact = "io.github.pseudomuto:protoc-gen-doc:1.5.1"
        }
    }
    generateProtoTasks {
        all().forEach {
            it.plugins {
                id("grpc")
                id("grpckt")
                id("doc") {
                    option("markdown,api.md")
                }
            }
            it.builtins {
                id("kotlin")
            }
        }
    }
}

tasks.register<Copy>("copyApiDocs") {
    dependsOn("generateProto")

    from("build/generated/source/proto/main/doc/api.md")
    into("${rootProject.projectDir}/docs/docs/reference")

    doFirst {
        file("${rootProject.projectDir}/docs/docs/reference").mkdirs()
    }

    doLast {
        val dest = file("${rootProject.projectDir}/docs/docs/reference/api.md")
        var content = dest.readText()

        // Decode HTML entities that protoc-gen-doc emits (break MDX/URL parsing)
        content = content.replace("&#34;", "\"")
        content = content.replace("&#39;", "'")
        // Escape curly braces so MDX doesn't treat them as JSX expressions
        content = content.replace("{", "\\{")
        content = content.replace("}", "\\}")
        // Replace raw <a name="..."> anchors with markdown-compatible ids
        content = content.replace(Regex("""<a name="([^"]+)"></a>"""), "")
        // Replace <p> tags
        content = content.replace(Regex("""<p align="right"><a href="#top">Top</a></p>"""), "[Top](#top)")

        val frontmatter = """---
sidebar_label: API Reference
title: gRPC API Reference
description: Auto-generated API documentation for TypeStream gRPC services.
---

<!-- This file is auto-generated by protoc-gen-doc. Do not edit manually. -->

""".trimStart()
        dest.writeText(frontmatter + content)
    }
}
